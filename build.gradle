buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public' }
        jcenter()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.0+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

repositories {
    maven { url = 'https://repo.spongepowered.org/repository/maven-public' }
    maven { url = 'https://www.cursemaven.com' }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

version = '2.0.0-alpha.12'
group = 'msifeed.misca'
archivesBaseName = 'misca'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'
compileJava.options.encoding = compileTestJava.options.encoding = 'UTF-8'

minecraft {
    mappings channel: 'stable', version: '39-1.12'

    runs {
        client {
            workingDirectory project.file('run')
            args(
                    '--tweakClass', 'org.spongepowered.asm.launch.MixinTweaker',
                    '--mixin', 'mixins.misca.json',
                    '--username', 'msifeed'
            )
            jvmArg '-Dfml.coreMods.load=msifeed.misca.mixins.MiscaDepsPlugin'
        }

        server {
            workingDirectory project.file('run')
            args(
                    '--tweakClass', 'org.spongepowered.asm.launch.MixinTweaker',
                    '--mixin', 'mixins.misca.json'
            )
            jvmArg '-Dfml.coreMods.load=msifeed.misca.mixins.MiscaDepsPlugin'
        }
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'
    embed 'org.spongepowered:mixin:0.8.2'

//    implementation fg.deobf("curse.maven:botania-225643:2846950")
    implementation fg.deobf("curse.maven:techguns-244201:2958103")
    implementation fg.deobf("curse.maven:baubles-227083:2518667")
    implementation fg.deobf("curse.maven:flans-mod-5-5-2-317310:3195600")
    implementation fg.deobf("curse.maven:electroblobs-wizardry-265642:3189062")
    compileOnly fg.deobf("curse.maven:thaumcraft-223628:2629023")
}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceSets {
    main {
        ext.refMap = "misca.refmap.json"
        output.resourcesDir = file('out/production/classes')
    }
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }

    manifest.attributes(
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': 0,
            'MixinConfigs': 'mixins.misca.json',

            'FMLCorePlugin': 'msifeed.misca.mixins.MiscaDepsPlugin',
            'FMLCorePluginContainsFMLMod': 'true',
            'ForceLoadAsMod': 'true',
    )
}

// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')

task copyLatestJar(type: Copy) {
    from jar
    into 'build/libs'
    rename { String fileName -> return archivesBaseName + '-latest.jar' }
}

build.dependsOn copyLatestJar