buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://files.minecraftforge.net/maven' }
        maven { url 'https://repo.spongepowered.org/repository/maven-public' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

repositories {
    maven { url 'https://repo.spongepowered.org/repository/maven-public' }
    maven { url 'https://www.cursemaven.com' }
    maven { url 'https://dvs1.progwml6.com/files/maven' }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = '2.2.5'
group = 'msifeed.misca'
archivesBaseName = 'misca'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'
compileJava.options.encoding = compileTestJava.options.encoding = 'UTF-8'

minecraft {
    version = '1.12.2-14.23.5.2847'
    mappings = 'stable_39'

    useDepAts = true
    makeObfSourceJar = false

    runDir = 'run'
    setClientRunArgs([
            "--tweakClass", "org.spongepowered.asm.launch.MixinTweaker",
            "--mixin", "mixins.misca.json",
            "--username", "msifeed",
    ])
    setServerRunArgs([
            "--tweakClass", "org.spongepowered.asm.launch.MixinTweaker",
            "--mixin", "mixins.misca.json",
    ])
    setClientJvmArgs([
            "-Dfml.coreMods.load=msifeed.misca.mixins.MiscaDepsPlugin",
            "-Dlog4j.configurationFile=log4j2_fix.xml", // Fixes chat in 2021
    ])
    setServerJvmArgs([
            "-Dfml.coreMods.load=msifeed.misca.mixins.MiscaDepsPlugin",
            "-Dlog4j.configurationFile=log4j2_fix.xml", // Fixes chat in 2021
    ])
}

configurations {
    embed
    compile.extendsFrom(embed)
}

sourceSets {
    main {
        ext.refMap = "misca.refmap.json"
        resources {
            srcDir 'resources'
        }
    }
}

dependencies {
    embed 'org.spongepowered:mixin:0.8.2'
    embed 'org.mariadb.jdbc:mariadb-java-client:2.7.2'
    embed 'org.mongodb:mongodb-driver-sync:4.2.2'

    compile "curse.maven:techguns-244201:2958103"
    compile "curse.maven:baubles-227083:2518667"
    compile "curse.maven:flans-mod-5-5-2-317310:3195600"
    compile "curse.maven:electroblobs-wizardry-265642:3189062"
    compile "curse.maven:thaumcraft-223628:2629023"
    compile "slimeknights.mantle:Mantle:1.12-1.3.3.55"
    compile "slimeknights:TConstruct:1.12.2-2.13.0.190"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }

    manifest.attributes(
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': 0,
            'MixinConfigs': 'mixins.misca.json',

            'FMLCorePlugin': 'msifeed.misca.mixins.MiscaDepsPlugin',
            'FMLCorePluginContainsFMLMod': 'true',
            'ForceLoadAsMod': 'true',
    )
}

task copyLatestJar(type: Copy) {
    from jar
    into 'build/libs'
    rename { String fileName -> return archivesBaseName + '-latest.jar' }
}

build.dependsOn copyLatestJar