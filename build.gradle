buildscript {
    repositories {
        mavenCentral()
        maven {
            name "forge"
            url "http://files.minecraftforge.net/maven"
        }
        maven {
            name "sonatype"
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

repositories {
    maven { url uri("lib") }
}


apply plugin: 'forge'
apply plugin: 'idea'

version = "${mod_version}"
group = "ru.ariadna.misca"
archivesBaseName = "misca"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "eclipse"

    replaceIn "Misca.java"
    replace "@VERSION@", project.version
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade 'com.moandjiezana.toml:toml4j:0.7.1'
    compile "net.ilexiconn:llibrary:1.5.1-1.7.10:dev"
    runtime 'mysql:mysql-connector-java:5.1.41'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'com/google/gson/**', 'META-INF', 'META-INF/**'
        }
    }
}

// Fix IDEA resources
idea { module { inheritOutputDirs = true } }